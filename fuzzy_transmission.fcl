FUNCTION_BLOCK
VAR_INPUT
speed : REAL;
throttle : REAL;
rpm : REAL;
END_VAR
VAR_OUTPUT
gear : REAL;
END_VAR

FUZZIFY speed
TERM verySlow := (0, 1) (10, 1) (20, 0.50) (30, 0);
TERM slow := (10, 0) (20, 0.50) (30, 1) (40, 1) (50, 0.50) (60, 0);
TERM avg := (40, 0) (50, 0.50) (60, 1) (80, 0.50) (100, 0);
TERM fast := (60, 0) (80, 0.50) (100, 1) (110, 1) (130, 0.50) (150, 0);
TERM veryFast := (110, 0) (130, 0.50) (150, 1) (170, 1) (200, 0.5) (230, 0);
TERM uberFast := (170, 0) (200, 0.50) (230, 1) (280, 1);
END_FUZZIFY

FUZZIFY throttle
TERM low := (0, 1) (15, 1) (30, 0.50) (45, 0);
TERM med := (15, 0) (30, 0.50) (45, 1) (55, 1) (70, 0.50) (85, 0);
TERM full := (55, 0) (70, 0.50) (85, 1) (100, 1);
END_FUZZIFY

FUZZIFY rpm
TERM veryLow := (0, 1) (1200, 1) (1500, 0.5) (1800, 0);
TERM low := (1200, 0) (1500, 0.5) (1800, 1) (2400, 1) (2700, 0.5) (3000, 0);
TERM med := (2400, 0) (2700, 0.5) (3000, 1) (3700, 1) (4000, 0.5) (4300, 0);
TERM high := (3700, 0) (4000, 0.50) (4300, 1) (5200, 1) (5500, 0.50) (5800, 0);
TERM veryHigh := (5200, 0) (5500, 0.50) (5800, 1) (7000, 1);
END_FUZZIFY

DEFUZZIFY gear
TERM veryLow := (0, 0) (0.5, 0.5) (1, 1) (1.5, 0.5) (2, 0);
TERM low := (1, 0) (1.5, 0.5) (2, 1) (3, 0.50) (4, 0);
TERM med := (2, 0) (3, 0.50) (4, 1) (4.5, 0.5) (5, 0);
TERM high := (4, 0) (4.5, 0.50) (5, 1) (5.5, 0.5) (6, 0);
TERM veryHigh := (5, 0) (5.5, 0.50) (6, 1) (6.5, 0.5) (7, 0);
METHOD : COG;
DEFAULT := 1;
END_DEFUZZIFY

RULEBLOCK first
AND: MIN;
ACT: MIN;

RULE 0: IF speed IS verySlow THEN gear IS veryLow;

RULE 1: IF speed IS slow AND throttle IS low THEN gear IS med;
RULE 2: IF speed IS slow AND throttle IS med THEN gear IS low;
RULE 3: IF speed IS slow AND throttle IS full THEN gear IS veryLow;

RULE 4: IF speed IS avg AND throttle IS low THEN gear IS high;
RULE 5: IF speed IS avg AND throttle IS med THEN gear IS med;
RULE 6: IF speed IS avg AND throttle IS full THEN gear IS low;

RULE 7: IF speed IS fast AND throttle IS low THEN gear IS veryHigh;
RULE 8: IF speed IS fast AND throttle IS med THEN gear IS high;
RULE 9: IF speed IS fast AND throttle IS full THEN gear IS med;

RULE 10: IF speed IS veryFast AND throttle IS low THEN gear IS veryHigh;
RULE 11: IF speed IS veryFast AND throttle IS med THEN gear IS veryHigh;
RULE 12: IF speed IS veryFast AND throttle IS full THEN gear IS high;

RULE 13: IF speed IS uberFast THEN gear IS veryHigh;

RULE 14: IF speed IS slow AND rpm IS veryHigh THEN gear IS low;
RULE 15: IF speed IS avg AND rpm IS veryHigh THEN gear IS med;
RULE 16: IF speed IS fast AND rpm IS veryHigh THEN gear IS high;

RULE 17: IF speed IS veryFast AND rpm IS veryHigh THEN gear IS veryHigh;

END_RULEBLOCK
END_FUNCTION_BLOCK