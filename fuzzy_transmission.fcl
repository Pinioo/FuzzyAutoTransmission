FUNCTION_BLOCK
VAR_INPUT
speed : REAL;
throttle : REAL;
tilt : REAL;
END_VAR
VAR_OUTPUT
gear : REAL;
END_VAR

FUZZIFY speed
TERM verySlow := (0, 1) (10, 1) (15, 0.50) (20, 0);
TERM slow := (10, 0) (15, 0.50) (20, 1) (25, 1) (35, 0.50) (45, 0);
TERM avg := (25, 0) (35, 0.50) (45, 1) (70, 1) (90, 0.50) (110, 0);
TERM fast := (70, 0) (90, 0.50) (110, 1) (130, 0.50) (150, 0);
TERM veryFast := (110, 0) (130, 0.50) (150, 1) (160, 1) (190, 0.5) (220, 0);
TERM uberFast := (160, 0) (190, 0.50) (220, 1) (280, 1);
END_FUZZIFY

FUZZIFY throttle
TERM low := (0, 1) (15, 1) (30, 0.50) (45, 0);
TERM med := (15, 0) (30, 0.50) (45, 1) (55, 1) (70, 0.50) (85, 0);
TERM full := (55, 0) (70, 0.50) (85, 1) (100, 1);
END_FUZZIFY

FUZZIFY tilt
TERM down := (-15, 1) (-10, 1) (-6, 0.5) (-2, 0);
TERM flat := (-10, 0) (-6, 0.5) (-2, 1) (2, 1) (6, 0.5) (10, 0);
TERM up := (2, 0) (6, 0.5) (10, 1) (15, 1);
END_FUZZIFY

DEFUZZIFY gear
TERM veryLow := (0, 0) (0.5, 0.5) (1, 1) (1.5, 0.5) (2, 0);
TERM low := (1, 0) (1.5, 0.5) (2, 1) (2.5, 0.50) (3, 0);
TERM med := (2, 0) (2.5, 0.50) (3, 1) (4, 1) (4.5, 0.5) (5, 0);
TERM high := (4, 0) (4.5, 0.50) (5, 1) (5.5, 0.5) (6, 0);
TERM veryHigh := (5, 0) (5.5, 0.50) (6, 1) (6.5, 0.5) (7, 0);
METHOD : COG;
DEFAULT := 1;
END_DEFUZZIFY

RULEBLOCK first
AND: MIN;
ACT: MIN;

RULE 0: IF speed IS verySlow THEN gear IS veryLow;

RULE 1: IF speed IS slow AND throttle IS low THEN gear IS med;
RULE 2: IF speed IS slow AND throttle IS med THEN gear IS low;
RULE 3: IF speed IS slow AND throttle IS full THEN gear IS veryLow;

RULE 4: IF speed IS avg AND throttle IS low THEN gear IS high;
RULE 5: IF speed IS avg AND throttle IS med THEN gear IS med;
RULE 6: IF speed IS avg AND throttle IS full THEN gear IS low;

RULE 7: IF speed IS fast AND throttle IS low THEN gear IS veryHigh;
RULE 8: IF speed IS fast AND throttle IS med THEN gear IS high;
RULE 9: IF speed IS fast AND throttle IS full THEN gear IS med;

RULE 10: IF speed IS veryFast AND throttle IS low THEN gear IS veryHigh;
RULE 11: IF speed IS veryFast AND throttle IS med THEN gear IS veryHigh;
RULE 12: IF speed IS veryFast AND throttle IS full THEN gear IS high;

RULE 13: IF speed IS uberFast THEN gear IS veryHigh;

RULE 14: IF tilt IS up AND speed IS avg THEN gear IS low;
RULE 15: IF tilt IS up AND speed IS fast THEN gear IS med;
RULE 16: IF tilt IS up AND speed IS veryFast THEN gear IS high;

RULE 17: IF tilt IS flat AND speed IS avg THEN gear IS med;
RULE 18: IF tilt IS flat AND speed IS fast THEN gear IS high;

RULE 19: IF tilt IS down AND speed IS slow THEN gear IS med;
RULE 20: IF tilt IS down AND speed IS avg THEN gear IS high;
RULE 21: IF tilt IS down AND speed IS fast THEN gear IS veryHigh;

END_RULEBLOCK
END_FUNCTION_BLOCK